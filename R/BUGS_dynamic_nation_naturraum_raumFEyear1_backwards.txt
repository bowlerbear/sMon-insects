
  model{
    
    # State model
    
    for (i in 1:nsite){ 
    
    #final year occupancy, model
    
    #data distribution
    z[i,nyear] ~ dbern(psi1[i])
    
    logit(psi1[i]) <- random.site[i]
    #r.a[raumS[i]] + cr.a[craumS[i]] + 

    for (t in 1:(nyear-1)){
    
    #data distribution
      z[i,t] ~ dbern(muZ[i,t]) 
    #overall model
      muZ[i,t] <- (z[i,t+1] - colonize[t] +0.001)/(persist[t] - colonize[t] + 0.001)

      }
    }   
    
    ### Observation Model
    for(j in 1:nvisit) {
    y[j] ~ dbern(Py[j]) #data is Y
    Py[j]<- z[site[j],year[j]]*p[j] 
    
    #detection model:
    logit(p[j]) <-  mup[year[j]] + 
                    mu.phenol * yday[j] + 
                    mu.phenol2 * pow(yday[j],2)+
                    effort.p * Effort[j] +
                    single.p * singleList[j]
    } 
    
    # Derived parameters
    
    #metapopulation-level
    for(t in 1:nyear){  
      psi.fs[t] <- sum(z[1:nsite,t])/nsite  
    } 

    # overall trend in occpuancy
    sumY <- sum(psi.fs[1:nyear])
    for (t in 1:nyear) {
      sumxy[t] <- psi.fs[t]*t
    }
    sumXY <- sum(sumxy[1:nyear])
    regres.psi <- (sumXY - ((sumX*sumY)/nyear))/(sumX2 - ((sumX*sumX)/nyear))#linear regression trend

    meanPersist <- mean(persist)
    meanColonize <- mean(colonize)
    
    #detection probability
    mean.p <- mean(p)

    #Priors 
    
    # State model priors
    
    ##########################################
    #year 1 spatial effects
    
    #cr.a fixed effects for couarse naturraum
    for (s in 1:ncraum) {
      cr.a[s] ~ dnorm(0,0.001)     
    } 

    #r.a random effects for naturraum
    for (s in 1:nraum) {
      r.a[s] ~ dnorm(0, tau.r.a)       
    } 
    tau.r.a <- 1/(sd.r.a * sd.r.a)
    sd.r.a ~ dt(0, 1, 1)T(0,)

    #site random effects
    for (s in 1:nsite) {
      random.site[s] ~ dnorm(0, tau.site.a)       
    } 
    tau.site.a <- 1/(sd.site.a * sd.site.a)
    sd.site.a ~ dt(0, 1, 1)T(0,)
    
    ################################
    
    #change priors for persistence and colonization
    
    for(t in 1:(nyear-1)){
      persist[t] ~ dunif(0.01,0.99)
      colonize[t] ~ dunif(0.01,0.99)
    }
    
    ################################
    #Observation model priors
    
    #year1
    p.mup[1] ~ dunif(0.05,0.95)
    mup[1] <- logit(p.mup[1])
    
    #other years
    for(t in 2:nyear){
      mup[t] ~ dnorm(mup[t-1], tau.ap)
    }
    tau.ap <- 1/(sd.ap * sd.ap)
    sd.ap ~ dt(0, 1, 1)T(0,) 
    
    #effort effects
    effort.p ~ dnorm(0, 0.01)
    single.p ~ dnorm(0, 0.01)
    
    #phenology
    mu.phenol ~ dnorm(0,0.01)
    mu.phenol2 ~ dnorm(0,0.01)
    
  }
    
