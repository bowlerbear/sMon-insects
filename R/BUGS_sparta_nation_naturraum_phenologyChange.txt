
  model{
    
    # State model
    
    for (i in 1:nsite){ 
      for (t in 1:nyear){
    
    #data distribution
      z[i,t] ~ dbern(muZ[i,t]) 

    # model
      logit(muZ[i,t]) <- mu.t[t] + cr.a[craumS[i]] + r.a[raumS[i]] + eta[i]
      
      }
    }   
    
    ### Observation Model
    for(j in 1:nvisit) {
    
    y[j] ~ dbern(Py[j]) #data is Y 
    Py[j]<- z[site[j],year[j]]*p[j] 
    
    #detection model:
    logit(p[j]) <-  mup[year[j]] + 
                    phenol[year[j]] * yday[j] + #allow the slopes to change among years
                    phenol2[year[j]] * yday2[j]+ #allow the slopes to change among years
                    effort.p * Effort[j] +
                    single.p * singleList[j]
    } 
    
    # Derived parameters
    
    #metapopulation-level
    for(t in 1:nyear){  
      psi.fs[t] <- sum(z[1:nsite,t])/nsite  
    } 

    # overall trend in occpuancy
    sumY <- sum(psi.fs[1:nyear])
    for (t in 1:nyear) {
      sumxy[t] <- psi.fs[t]*t
    }
    sumXY <- sum(sumxy[1:nyear])
    regres.psi <- (sumXY - ((sumX*sumY)/nyear))/(sumX2 - ((sumX*sumX)/nyear))#trend

    
    #detection probability
    mean.p <- mean(p)
    
    #Priors 
    
    # State model priors
    
    #year
    
    #year 1
    mu.prop ~ dunif(0,1)
    mu.t[1] <- logit(mu.prop)
    
    #other years
    for(t in 2:nyear){
      mu.t[t] ~ dnorm(mu.t[t-1], tau.a)
    }
    tau.a <- 1/(sd.a * sd.a)
    sd.a ~ dt(0, pow(2.5,-2), 1)T(0,) 

    #cr.a random effects for coarse naturraum
    for (s in 1:ncraum) {
      cr.a[s] ~ dnorm(0, tau.cr.a)       
    } 
    tau.cr.a <- 1/(sd.cr.a * sd.cr.a)
    sd.cr.a ~ dt(0, pow(2.5,-2), 1)T(0,)
    
    #r.a random effects for naturraum
    for (s in 1:nraum) {
      r.a[s] ~ dnorm(0, tau.r.a)       
    } 
    tau.r.a <- 1/(sd.r.a * sd.r.a)
    sd.r.a ~ dt(0, pow(2.5,-2), 1)T(0,)
    
    #site effects
    for (i in 1:nsite) {
      eta[i] ~ dnorm(0, tau.eta)       
    } 
    tau.eta <- 1/(sd.eta * sd.eta) 
    sd.eta ~ dt(0, pow(2.5,-2), 1)T(0,)
  
    #####################################################
    
    #Observation model priors

    #year
    for(t in 1:nyear){
      mup[t] ~ dnorm(mu.p, tau.ap)
    }
    p.prop ~ dunif(0,1)
    mu.p <- logit(p.prop) 
    tau.ap <- 1/(sd.ap * sd.ap)
    sd.ap ~ dt(0, pow(2.5,-2), 1)T(0,)  
    
    #effort effects
    effort.p ~ dnorm(0, 0.01)
    single.p ~ dnorm(0, 0.01)
    
    #phenology effects
    for(t in 1:nyear){
      phenol[t] ~ dnorm(mu.phenol,tau.phenol)
      phenol2[t] ~ dnorm(mu.phenol2,tau.phenol2)
    }
    mu.phenol ~ dnorm(0,0.01)
    mu.phenol2 ~ dnorm(0,0.01)
    tau.phenol <- 1/(sd.phenol * sd.phenol)
    tau.phenol2 <- 1/(sd.phenol2 * sd.phenol2)
    sd.phenol ~ dt(0, pow(2.5,-2), 1)T(0,)  
    sd.phenol2 ~ dt(0, pow(2.5,-2), 1)T(0,)  
    
  }
    
