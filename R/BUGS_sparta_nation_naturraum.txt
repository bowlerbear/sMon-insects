
  model{
    
    # State model
    
    for (i in 1:nsite){ 
      for (t in 1:nyear){
    
    #data distribution
      z[i,t] ~ dbern(muZ[i,t]) 

    # model
      logit(muZ[i,t]) <- mu[t] + eta[i]
      
      }
    }   
    
    ### Observation Model
    for(j in 1:nvisit) {
    
    y[j] ~ dbern(Py[j]) #data is Y 
    Py[j]<- z[site[j],year[j]]*p[j] 
    
    #detection model:
    logit(p[j]) <-  mup[year[j]] + 
                    mu.phenol * yday[j] + 
                    mu.phenol2 * yday2[j]+
                    effort.p * Effort[j] +
                    single.p * singleList[j]
    } 
    
    # Derived parameters
    
    #metapopulation-level
    for(t in 1:nyear){  
      psi.fs[t] <- sum(z[1:nsite,t])/nsite  
    } 

    
    #detection probability

    #Priors 
    
    # State model priors
    
    #year
    
    #year 1
    mu[1] ~ dnorm(0, 0.001)
    
    #other years
    for(t in 2:nyear){
      mu[t] ~ dnorm(mu[t-1], tau.a)
    }
    tau.a <- 1/(sd.a * sd.a)
    sd.a ~ dt(0, 1, 1)T(0,) 

    #site effects
    for (i in 1:nsite) {
      eta[i] ~ dnorm(0, tau.eta)       
    } 
    tau.eta <- 1/(sd.eta * sd.eta) 
    sd.eta ~ dt(0, 1, 1)T(0,)
  
    #####################################################
    
    #Observation model priors

    #year 1
    mup[1] ~ dnorm(0, 0.001)
    
    #other years
    for(t in 2:nyear){
      mup[t] ~ dnorm(mup[t-1], tau.ap)
    }
    tau.ap <- 1/(sd.ap * sd.ap)
    sd.ap ~ dt(0, 1, 1)T(0,)  
    
    #effort effects
    effort.p ~ dnorm(0, 0.001)
    single.p ~ dnorm(0, 0.001)
    
    #phenology effects
    mu.phenol ~ dnorm(0,0.001)
    mu.phenol2 ~ dnorm(0,0.001)
 
    
  }
    
