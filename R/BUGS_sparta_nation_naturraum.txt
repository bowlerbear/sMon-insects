
  model{
    
    # State model
    
    for (i in 1:nsite){ 
      for (t in 1:nyear){
    
    #data distribution
      z[i,t] ~ dbern(muZ[i,t]) 

    # model
      logit(muZ[i,t]) <- mu[t] + cr.a[craumS[i]] + r.a[raumS[i]]+ eta[i]
      
      }
    }   
    
    ### Observation Model
    for(j in 1:nvisit) {
    
    y[j] ~ dbern(Py[j]) #data is Y
    Py[j]<- z[site[j],year[j]]*p[j] 
    
    #detection model:
    logit(p[j]) <-  mup[year[j]] + 
                    lp[craum[j]] +
                    phenol.s[craum[j]] * yday[j] + 
                    phenol2.s[craum[j]] * yday2[j]+
                    effort.p * Effort[j] +
                    single.p * singleList[j]
    } 
    
    # Derived parameters
    
    #metapopulation-level
    for(t in 1:nyear){  
      psi.fs[t] <- sum(z[1:nsite,t])/nsite  
    } 

    # overall trend in occpuancy
    sumY <- sum(psi.fs[1:nyear])
    for (t in 1:nyear) {
      sumxy[t] <- psi.fs[t]*t
    }
    sumXY <- sum(sumxy[1:nyear])
    regres.psi <- (sumXY - ((sumX*sumY)/nyear))/(sumX2 - ((sumX*sumX)/nyear))#linear regression tren

    
    #detection probability
    mean.p <- mean(p)

    #Priors 
    
    # State model priors
    
    #year
    
    #year 1
    mu[1] ~ dnorm(0, 0.01)
    #other years
    for(t in 2:nyear){
      mu[t] ~ dnorm(mu[t-1], tau.a)
    }
    tau.a <- 1/(sd.a * sd.a)
    sd.a ~ dt(0, 1, 1)T(0,) 

    #cr.a random effects for naturraum
    for (s in 1:ncraum) {
      cr.a[s] ~ dnorm(0, tau.cr.a)       
    } 
    tau.cr.a <- 1/(sd.cr.a * sd.cr.a)
    sd.cr.a ~ dt(0, 1, 1)T(0,)
    
    #r.a random effects for naturraum
    for (s in 1:nraum) {
      r.a[s] ~ dnorm(0, tau.r.a)       
    } 
    tau.r.a <- 1/(sd.r.a * sd.r.a)
    sd.r.a ~ dt(0, 1, 1)T(0,)

    #site effects
    for (i in 1:nsite) {
      eta[i] ~ dnorm(0, tau.eta)       
    } 
    tau.eta <- 1/(sd.eta * sd.eta) 
    sd.eta ~ dt(0, 1, 1)T(0,)
  
    #####################################################
    
    #Observation model priors
    
    #year
    
    #year 1
    mup[1] ~ dnorm(0, 0.01)
    #other years
    for(t in 2:nyear){
      mup[t] ~ dnorm(mup[t-1], tau.ap)
    }
    tau.ap <- 1/(sd.ap * sd.ap)
    sd.ap ~ dt(0, 1, 1)T(0,)  

    #naturraum-level variaion - random effects
    for(s in 1:ncraum){
      lp[s]  ~ dnorm(0,tau.lp)
    }
    tau.lp <- 1/(sd.lp * sd.lp) 
    sd.lp ~ dt(0, 1, 1)T(0,)
    
    #effort effects
    effort.p ~ dnorm(0, 0.01)
    single.p ~ dnorm(0, 0.01)
    
    #random slopes for phenology
    for(s in 1:ncraum){
      phenol.s[s] ~ dnorm(mu.phenol, tau.phenol)
      phenol2.s[s] ~ dnorm(mu.phenol2, tau.phenol2)
    }
    mu.phenol ~ dnorm(0,0.01)
    mu.phenol2 ~ dnorm(0,0.01)
    tau.phenol <- 1/(sd.phenol * sd.phenol)
    tau.phenol2 <- 1/(sd.phenol2 * sd.phenol2)
    sd.phenol ~ dt(0, 1, 1)T(0,)
    sd.phenol2 ~ dt(0, 1, 1)T(0,) 
    
  }
    
