
  model{
    
    # State model
    
    for (i in 1:nsite){ 
    
    #first year/initial occupancy, model
    
    #data distribution
    z[i,1] ~ dbern(psi1[i])
    
    logit(psi1[i]) <- r.a[raumS[i]] + cr.a[craumS[i]] + random.site[i]

    for (t in 2:nyear){
    
    #data distribution
      z[i,t] ~ dbern(muZ[i,t]) 
    
    #overall model
      muZ[i,t] <- persist[t-1]*z[i,t-1] + colonize[t-1]*(1-z[i,t-1])

      }
    }   
    
    ### Observation Model
    for(j in 1:nvisit) {
    y[j] ~ dbern(Py[j]) #data is Y
    Py[j]<- z[site[j],year[j]]*p[j] 
    
    #detection model:
    logit(p[j]) <-  mup[year[j]] + 
                    lp[craum[j]] +
                    lp.r[raum[j]] +
                    phenol.s[craum[j]] * yday[j] + 
                    phenol2.s[craum[j]] * pow(yday[j],2)+
                    effort.p * Effort[j] +
                    single.p * singleList[j]
    } 
    
    # Derived parameters
    
    #metapopulation-level
    for(t in 1:nyear){  
      psi.fs[t] <- sum(z[1:nsite,t])/nsite  
    } 

    # overall trend in occpuancy
    sumY <- sum(psi.fs[1:nyear])
    for (t in 1:nyear) {
      sumxy[t] <- psi.fs[t]*t
    }
    sumXY <- sum(sumxy[1:nyear])
    regres.psi <- (sumXY - ((sumX*sumY)/nyear))/(sumX2 - ((sumX*sumX)/nyear))#linear regression trend


    meanPersist <- mean(persist)
    meanColonize <- mean(colonize)
    
    #detection probability
    mean.p <- mean(p)

    #Priors 
    
    # State model priors
    
    ##########################################
    #year 1 spatial effects
    
    #cr.a fixed effects for couarse naturraum
    for (s in 1:ncraum) {
      cr.a.prop[s] ~ dunif(0.000001,priorS1[s])
      cr.a[s] <- logit(cr.a.prop[s])     
    } 

    #r.a random effects for naturraum
    for (s in 1:nraum) {
      r.a[s] ~ dnorm(0, tau.r.a)       
    } 
    tau.r.a <- 1/(sd.r.a * sd.r.a)
    sd.r.a ~ dt(0, 1, 1)T(0,)

    #site random effects
    for (s in 1:nsite) {
      random.site[s] ~ dnorm(0, tau.site.a)       
    } 
    tau.site.a <- 1/(sd.site.a * sd.site.a)
    sd.site.a ~ dt(0, 1, 1)T(0,)
    
    ################################
    
    #change priors for persistence and colonization
    #random walk model
    
    persist[1] ~ dunif(0,1)
    colonize[1] ~ dunif(0,1)
    logit.persist[1] <- logit(persist[1])
    logit.colonize[1] <- logit(colonize[1])
    
    for(t in 2:(nyear-1)){
      logit.persist[t] ~ dnorm(logit.persist[t-1],tau.rwp)
      logit.colonize[t] ~ dnorm(logit.colonize[t-1],tau.rwc)
      persist[t] <- ilogit(logit.persist[t])
      colonize[t] <- ilogit(logit.colonize[t])
    }
    
    tau.rwp <- 1/(sd.rwp * sd.rwp)
    sd.rwp ~ dt(0, 1, 1)T(0,) 
    
    tau.rwc <- 1/(sd.rwc * sd.rwc)
    sd.rwc ~ dt(0, 1, 1)T(0,) 
    
    ################################
    #Observation model priors
    
    #intercept
    int.dp ~ dnorm(0,0.01)
    
    #random year effects
    for(t in 1:nyear){
      mup[t]  ~ dnorm(0,tautp)
    }
    tautp <- 1/(sdtp * sdtp)
    sdtp ~ dt(0, 1, 1)T(0,)
    
    #naturraum-level variaion - fixed effects
    for(s in 1:ncraum){
      lp[s]  ~ dnorm(0,0.01)
    }

    #random site effects
    for(i in 1:nraum){
      lp.r[i]  ~ dnorm(0,tau.rp)
    }
    tau.rp <- 1/(sd.rp * sd.rp)
    sd.rp ~ dt(0, 1, 1)T(0,)
    
    #effort effects
    effort.p ~ dnorm(0, 0.01)
    single.p ~ dnorm(0, 0.01)
    
    #random slopes for phenology
    for(s in 1:ncraum){
      phenol.s[s] ~ dnorm(mu.phenol, tau.phenol)
      phenol2.s[s] ~ dnorm(mu.phenol2, tau.phenol2)
    }
    mu.phenol ~ dnorm(0,0.01)
    mu.phenol2 ~ dnorm(0,0.01)
    tau.phenol <- 1/(sd.phenol * sd.phenol)
    tau.phenol2 <- 1/(sd.phenol2 * sd.phenol2)
    sd.phenol ~ dt(0, 1, 1)T(0,)
    sd.phenol2 ~ dt(0, 1, 1)T(0,) 
    
  }
    
